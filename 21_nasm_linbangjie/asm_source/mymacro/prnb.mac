; ********************* prnb.mac ********************
;
; prnb bmem, byteCountValue
; 將bmem記憶體開始，共byteCountValue個位元組內含值
; 以位元形式顯示於螢幕
;   bmem           : 字元所存放記憶體位址 (傳址)
;   byteCountValue : 共byteCountValue個位元組 (傳值)
;
%ifndef PRNB_MAC
%define PRNB_MAC
%MACRO prnb    2             ;二個參數
       PUSHA                 ;儲存原來暫存器資料
       MOV     CX, %2        ;字元數
       MOV     BX, 0         ;第一個字元
%%loop2:
       MOV     DH, [%1+BX]   ;要顯示的字元存入DL
       PUSH    CX
       MOV     CX, 8         ;CX=8個位元
%%loop3:
       SHL     DH, 1         ;位元左移一位
       JC      %%bit1        ;移出的位元的值為1?
       MOV     DL, '0'       ;否
       JMP     %%next
%%bit1:
       MOV     DL, '1'       ;是
%%next:
       MOV     AH, 02H       ;設定顯示字元功能
       INT     21H           ;顯示存於DL之字元
       LOOP    %%loop3       ;繼續8次(8位元)
       MOV     DL, ' '
       MOV     AH, 02H
       INT     21H           ;空一格
       POP     CX
       INC     BX            ;下一個字元
       LOOP    %%loop2       ;繼續
       POPA                  ;恢復原來暫存器資料
%ENDMACRO
%endif

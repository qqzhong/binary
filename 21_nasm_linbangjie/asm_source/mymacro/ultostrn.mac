; ********************* ultostrn.mac ********************
;
; ultostrn  dmem, smem, n, fillchar
;   將 dmem 雙字組位址內含以 n 位置轉成字串存入 smem 位址
;   有效數字左邊補上 fillchar 字元
; dmem     : 雙字組整數位址 (傳址)
; smem     : 字串位址 (傳址)
; n        : 指定顯示位數 (傳值)
; fillchar : 填入字元 (傳值)
;
%ifndef ULTOSTR_MAC
%define ULTOSTR_MAC
%MACRO  ultostrn  4
        PUSHA
        MOV     CX, %3         ;ASCII數字位數計數
        MOV     SI, 0
%%loop1:
        MOV     BYTE [%2+SI], %4
        INC     SI
        LOOP    %%loop1
;
        MOV     CX, %3           ;ASCII數字位數計數
        MOV     EAX, DWORD [%1]  ;EAX=雙字組位址內含
        MOV     EDX, 0           ;EDX=0
        MOV     EBX, 10          ;EBX=10
        MOV     SI, CX
        MOV     BYTE [%2+SI], '$'
        DEC     SI
%%loop2:
        DIV     EBX                ;EDX:EAX/EBX
        ADD     DL, 30H            ;餘數
        MOV     BYTE [%2+SI], DL
        DEC     SI
        MOV     EDX, 0             ;EDX=0
        CMP     EAX, 0
        LOOPNZ  %%loop2
        POPA
%ENDMACRO
%endif
